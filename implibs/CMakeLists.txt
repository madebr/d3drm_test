function(add_win32_implib TARGET)
    set(def "${CMAKE_CURRENT_SOURCE_DIR}/${TARGET}.def")
    set(mingw_def "${CMAKE_CURRENT_SOURCE_DIR}/${TARGET}-mingw.def")
    if(MINGW AND EXISTS "${mingw_def}")
        set(def "${mingw_def}")
    endif()
    add_library(${TARGET}-dll SHARED ${TARGET}.c "${def}")
    set_property(TARGET ${TARGET}-dll PROPERTY RUNTIME_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}")
    set_property(TARGET ${TARGET}-dll PROPERTY PREFIX "")
    set_property(TARGET ${TARGET}-dll PROPERTY OUTPUT_NAME "${TARGET}")
    if(MINGW)
        target_link_options(${TARGET}-dll PRIVATE "-Wl,--enable-stdcall-fixup")
    endif()
    add_library(${TARGET} INTERFACE)
    target_link_libraries(${TARGET} INTERFACE "$<TARGET_IMPORT_FILE:${TARGET}-dll>")
    target_include_directories(${TARGET} SYSTEM INTERFACE "${CMAKE_CURRENT_SOURCE_DIR}/include")
endfunction()

add_win32_implib(d3drm)
add_win32_implib(d3dxof)
